import java_cup.runtime.*;
import java.io.*;
import java.util.*;

/* Preliminaries to set up and use the scanner.  */
parser code {:
		Lexer lexer;

		public ArithParser(Lexer l) {
		    // As of CUP v0.11, need to pass Lexer to superclass
		    super(l);
		    lexer = l;
		}

		public void report_error(String message, Object info) {
		    System.err.println(message);
		}

		public void syntax_error(Symbol cur_token) {
		    System.err.print("Line " + lexer.getLine() +
				     " near char " + lexer.getChar() + ": ");
		    report_error("Syntax error", cur_token);
		    System.err.println ("Last token read is " +
					 lexer.getText());
		}
	    :};

init with {:
	  :};

scan with {:
		try {
		    return lexer.next_token();
		} catch (java.io.IOException ioe) {
		    System.out.println("Line " + lexer.getLine() + ", pos " +
				       lexer.getColumn() +
				       ": Unrecognised token <" +
				       lexer.getText() + ">");
		    System.out.println(ioe.getMessage());
		    throw ioe;
		}
	  :};

/* Terminals (tokens returned by the scanner). */

// special symbols
terminal LPAREN, RPAREN;

// arithmetic operators
terminal PLUS, MINUS, MUL, DIV, MOD, ASSIGN;

terminal LET, LBRACE, COMMA;
// *** add other token declarations for LET special form here
terminal RBRACE, IN;


// terminals with values
terminal Integer INTEGER;
terminal String VARIABLE;

/* Non terminals */
non terminal ArithProgram program;
non terminal StmtSequence stmtList;
non terminal Statement stmt;
non terminal StmtDefinition definition;

non terminal StmtLet letStmt;
non terminal ArrayList bindings;
non terminal ArrayList bindingsAux;
non terminal Binding binding;

non terminal Exp expression;
non terminal Exp term;
non terminal Exp factor;
non terminal empty;

/* Grammar rules */

program ::= stmtList:s {: RESULT = new ArithProgram(s); :};

stmtList ::= stmtList:lst stmt:s {:
		lst.add(s); 
		RESULT = lst;
		:} |
	     stmt:s {:
		RESULT = new StmtSequence(s);
		:};

stmt ::= definition:d {: RESULT = d; :} |
	 letStmt:l {: RESULT = l; :} |
	 expression:e {: RESULT = new Statement(e); :};

/* ************* Let statement production rules ***************** */
/* Complete this using "bindings" defined below */
letStmt ::= LET bindings:bs IN LPAREN stmtList:sl RPAREN{:
			RESULT = new StmtLet(bs, sl);
            :}; 

binding ::= VARIABLE:v ASSIGN expression:e {:
		RESULT = new Binding(v, e);
	:};

bindings ::= binding:b bindingsAux:bl {:
	    	  /* Fill in the missing line here */
                  bl.add(b);
		  RESULT = bl;
	     :} |
		empty {: RESULT = new ArrayList(); :};

bindingsAux ::= COMMA binding:b bindingsAux:bl {:
	     	       /* Fill this in */
                       bl.add(b);
                       RESULT = bl;
		:} |
		empty {: RESULT = new ArrayList(); :};

/* ************* End of Let statement production rules ***************** */

definition ::= VARIABLE:v ASSIGN expression:e {:
		   RESULT = new StmtDefinition(v, e);
		:};

expression ::= 	expression:e PLUS term:t {:
			RESULT = new ExpAdd(e, t); :} |
		expression:e MINUS term:t {:
			RESULT = new ExpSub(e, t); :} |
		term:t {: RESULT = t; :};

term ::= term:t MUL factor:f {:
		RESULT = new ExpMul(t, f); :} |
	 term:t DIV factor:f {:
		RESULT = new ExpDiv(t, f); :} |
	 term:t MOD factor:f {:
		RESULT = new ExpMod(t, f); :} |
	 factor:f {: RESULT = f; :};

factor ::= INTEGER:ilit {: RESULT = new ExpLit(ilit); :} |
	   VARIABLE:var {: RESULT = new ExpVar(var); :} |
	   LPAREN expression:e RPAREN {: RESULT = e; :}	;

empty ::= ;
